# Maintainer: vypxl <thomas at vypxl dot io>
pkgname=gamemaker-beta-bin
pkgver=2023.1100.0.435
pkgrel=1
url="https://gamemaker.io/"
arch=(x86_64)
pkgdesc="GameMaker makes designing games easy, regardless of your background or skill level. All you need is an idea!"
source=("https://gms.yoyogames.com/GameMaker-Beta-${pkgver}.deb"
        "LICENSE"
        "GameMaker-Beta.desktop.patch"
        "launcher.sh")
sha256sums=('7bc6ff9a5b6a2108a5de7984004468c4378951e540196b2dbb324b5de30fabaf'
            '4ed1e936a919ebdc4bcd4134e555a08e750326053fa3b5ede5fd35028984926e'
            'f2f223bf59e8e31f44e04c19b95996a68470bddb0b5faae5ed12ac2b0dcc149c'
            '8fb7dfa5ca68d7dc5fa6f033e799784502841a6d4aba7505a91bef6f1a909881')
license=("custom")
install=$pkgname.install
depends=("openssl" "openssh" "clang" "libxrandr" "libxxf86vm" "openal" "libglvnd" "mesa" "glu" "zlib" "curl" "ffmpeg" "zip" "unzip" "gtk3" "appimagetool" "linuxdeploy-appimage")
options=(!strip)

prepare() {
  bsdtar xf data.tar.xz

  # Patch desktop file to fix problem with environment during build process, and for GM to find linuxdeploy
  # https://github.com/YoYoGames/GameMaker-Bugs/issues/2204
  patch usr/share/applications/GameMaker-Beta.desktop < GameMaker-Beta.desktop.patch
}

package() {
  # Install linuxdeploy appimage symlink for GM to use instead of the raw binary
  # GameMaker needs the appimage, a repackaged version of the tool won't work.
  mkdir -p "$pkgdir"/opt/GameMaker-Beta/linuxdeploy
  ln -s /opt/appimages/linuxdeploy.AppImage "$pkgdir"/opt/GameMaker-Beta/linuxdeploy/linuxdeploy

  # Install GM files
  cp -R usr "$pkgdir"/
  cp -R opt "$pkgdir"/

  # Install License
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

  # Install launcher
  install -Dm755 launcher.sh "$pkgdir"/usr/bin/GameMaker
}

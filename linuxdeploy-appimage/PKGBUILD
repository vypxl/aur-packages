# Maintainer: vypxl <thomas at vypxl dot io>

pkgname=linuxdeploy-appimage
provides=(linuxdeploy)
conflicts=(linuxdeploy)
_pkgver=1-alpha-20231026-1
pkgver=$(echo "$_pkgver" | sed -e's/-/./g')
pkgrel=1
url="https://github.com/linuxdeploy/linuxdeploy"
arch=(x86_64)
pkgdesc="AppDir creation and maintenance tool. Featuring flexible plugin system."
source=("linuxdeploy-$_pkgver.AppImage"::"https://github.com/linuxdeploy/linuxdeploy/releases/download/$_pkgver/linuxdeploy-static-x86_64.AppImage")
noextract=("linuxdeploy-$_pkgver.AppImage")
sha256sums=('7e354fb6722b04ac1838e4bde0a49564518400dc9680a33f8d799e877f9f5f6a')
license=("MIT")
depends=(patchelf)
options=(!strip)

prepare() {
  chmod +x "$srcdir"/linuxdeploy-"$_pkgver".AppImage
  ./linuxdeploy-"$_pkgver".AppImage --appimage-extract usr
  ./linuxdeploy-"$_pkgver".AppImage --appimage-extract plugins/linuxdeploy-plugin-appimage/usr/bin/linuxdeploy-plugin-appimage
}

package() {
  install -Dm755 "$srcdir"/linuxdeploy-"$_pkgver".AppImage "$pkgdir"/opt/appimages/linuxdeploy.AppImage

  install -Dm755 "$srcdir"/squashfs-root/usr/bin/linuxdeploy "$pkgdir"/usr/bin/linuxdeploy
  install -Dm755 "$srcdir"/squashfs-root/plugins/linuxdeploy-plugin-appimage/usr/bin/linuxdeploy-plugin-appimage "$pkgdir"/usr/bin/linuxdeploy-plugin-appimage
  install -Dm644 "$srcdir"/squashfs-root/usr/share/applications/linuxdeploy.desktop "$pkgdir"/usr/share/applications/linuxdeploy.desktop
  install -Dm644 "$srcdir"/squashfs-root/usr/share/icons/hicolor/64x64/apps/linuxdeploy.png "$pkgdir"/usr/share/icons/hicolor/64x64/apps/linuxdeploy.png
}
